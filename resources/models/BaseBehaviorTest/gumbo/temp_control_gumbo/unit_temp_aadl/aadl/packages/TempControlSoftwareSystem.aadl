-- ===========================================================================================================
--
--    T e m p e r a t u r e    C o n t r o l    S y s t e m
--
--       SimpleTemp - illustrate a temperature data type with only degrees (not unit) as a field
--       Mixed - with mixed port catagories (event, data, event data)
--       UI - with a simple UI for an operator interface   
--
--       Platform - Slang/JVM -- this example is designed for the HAMR Slang/JVM platform.  
--         Other versions of this example illustrate Linux and seL4 platforms.
--
--       The package structure reflects a conceptual business ecosystem in which stakeholders exchange AADL
--       modeling elements to manage distributed development:
--         - temperature sensor and cooling fan components are acquired from suppliers other than the 
--            system integrator (thus, these components are located in separate packages delivered by those suppliers).
--         - the system integrator designs the temperature controller and operator interface for the system
--            (thus, those components are in this package) and integrates all components into a system.
--
--    Author: SAnToS Lab Team
--    Last modified: August 15, 2020
--
-- ===========================================================================================================
package TempControlSoftwareSystem
public
  with Data_Model; -- import modeling support libraries
  with TempSensor, CoolingFan; -- import packages of supplier components to be integrated 

  -- ===========================================================================================================
  --   T e m p C o n t r o l S y s t e m
  -- 
  --   Top-level component of the model representing the software system boundary.
  -- 
  -- ===========================================================================================================
  system TempControlSoftwareSystem
    features
      -- since this is the "top level" of the architecture representing the system
      -- boundary there are no ports/features declared to interact with other components 
      none;
  end TempControlSoftwareSystem;

  -- ===========================================================================================================
  --   T e m p C o n t r o l P r o c e s s 
  --   
  --   Process incorporating all the software of the system.
  -- 
  -- ===========================================================================================================
  process TempControlProcess
    features
      -- since this is the main process at the top level of the architecture and there are no other processes, 
      -- there are no ports/features declared to interact with other components.
      none;
  end TempControlProcess;



  -- ===========================================================================================================
  --
  --   S P O R A D I C   I M P L E M E N T A T I O N
  --
  -- ===========================================================================================================
  system implementation TempControlSoftwareSystem.s
    -- ==== S u b - c o m p o n e n t s ====	
    subcomponents
      -- For simplicity in this introductory example, a single process is used.
      -- On the Slang/JVM platform, process architecture does not affect code generation.
      -- However, for Linux and seL4 platforms, there are constraints on the structure 
      -- of processes and their contents because processes reflect separate memory address
      -- spaces, which are reflected in code generation, e.g., as distinct seL4 partitions (CAmkES components).
      -- The broader concept here is that the notion of AADL process is used by HAMR to 
      -- model partitioning/isolation of software elements.
      --
      -- In a more realistic model of this system, the integrator might want to partition the
      -- control logic (tempControl Thread) which is higher criticality and operator interface 
      -- which is lower criticality (and partitioning of the operator interface might help contain
      -- security attacks (intrusions).   Drivers for hardware elements (temp sensor and fan) might also
      -- be isolated from other components.
      tcproc: process TempControlProcess.s;
    annex resolute {**
      check HAMR_Level_1
    **};
  end TempControlSoftwareSystem.s;

  process implementation TempControlProcess.s
    -- ==== S u b - c o m p o n e n t s ====	
    subcomponents
      tempSensor: thread TempSensor::TempSensor.s; -- use qualified name to refer to element in different package 
      fan: thread CoolingFan::Fan.s;
      tempControl: thread TempControl.s;
      operatorInterface: thread OperatorInterface.s;
    -- ==== C o n n e c t i o n s  ====	
    connections
      ctTStoTC: port tempSensor.currentTemp -> tempControl.currentTemp;
      ctTStoOI: port tempSensor.currentTemp -> operatorInterface.currentTemp;
      tcTStoTC: port tempSensor.tempChanged -> tempControl.tempChanged;
      tcTStoOI: port tempSensor.tempChanged -> operatorInterface.tempChanged;
      fcTCtoF: port tempControl.fanCmd -> fan.fanCmd;
      faFtoTC: port fan.fanAck -> tempControl.fanAck;
      spOItoTC: port operatorInterface.setPoint -> tempControl.setPoint;
    flows
      -- The intent for the end-to-end flow below is to capture the control loop from the 
      -- temp sensor (sensing the environment) to the fan actuation (acting on the environment).
      -- This can be augmented with a latency constraint -- capturing constraints on the time
      -- from when temperature is sensed to the time when is it evaluated. 
      fanActuation: end to end flow tempSensor.cto -> ctTStoTC -> tempControl.ct2fc -> fcTCtoF -> fan.fc2sink;
      -- Capture the control loop from the setPoint expressed by the operator through the
      -- Operator Interface to the impact of a changed set point on environment actions
      -- (i.e., actuation of the fan).    
      setPointEffect: end to end flow operatorInterface.source2sp -> spOItoTC -> tempControl.sp2fc -> fcTCtoF -> fan.fc2sink;
  end TempControlProcess.s;

  -- ===========================================================================================================
  --
  --   T e m p C o n t r o l   Thread
  --
  --   The TempControl thread contains the control logic for the system (i.e., implements a "thermostat").
  -- 
  -- 
  -- ===========================================================================================================
  thread TempControl
    features
      -- ==== INPUTS ==== 
      currentTemp: in data port TempSensor::Temperature.i;
      tempChanged: in event port;
      fanAck: in event data port CoolingFan::FanAck;
      setPoint: in event data port SetPoint.i;
      -- ==== OUTPUTS ==== 
      fanCmd: out event data port CoolingFan::FanCmd;
    flows
      -- currentTemp, tempChanged, and setPoint all influence the value/action of the fanCmd, so there
      -- is a flow path from each to fanCmd.
      ct2fc: flow path currentTemp -> fanCmd; -- data flow from currentTemp to fanCmd
      tc2fc: flow path tempChanged -> fanCmd; -- control flow from tempChanged (arrival of tempChanged possibly triggers new command)
      sp2fc: flow path setPoint -> fanCmd; -- data flow - setPoint value influences value of fanCmd
      -- fanAck is processed internally for the moment (through logging) -- there is no feedback to the UI.
      -- So the TempControl component acts as an information sink for the fanAck.
      fa2sink: flow sink fanAck;
    properties
      -- the tempControl execution is activated by the arrival of messages on its input event and event data ports
      Dispatch_Protocol => Sporadic;
      -- the minimal separation between incoming events is 1 sec
      Period => 1 sec;

    -- The following contract notation is not fully supported by GUMBO tools yet.
    -- The comment expresses the desired state/goals for the toolchain. 
    annex GUMBODraft {**       
      state
        currentSetPoint: SetPoint.i;
        currentFanState: CoolingFan::FanCmd;
        latestTemp: TempSensor::Temperature_i;
                 
      integration   
        assume currentTempRange [Traceability TBD]:
           currentTemp.degrees >= -70.0F & currentTemp.degrees <= 180.0F;
          
      initialization
        modifies currentSetPoint, currentFanState, latestTemp;
        guarantee "defaultSetPoint":
          currentSetPoint.low.degrees == 70.0F and currentSetPoint.high.degrees == 80.0F;
        guarantee "defaultFanState":
          currentFanState == FanCmd.Off;
        guarantee "defaultLatestTemp":
          latestTemp == 72.0F;
      
      compute  // behaviors for COMPUTE Entrypoint
        modifies currentSetPoint, currentFanState, latestTemp;
    
        --    ---------------------------
        --     Compute Entry Point General Clauses
        --       The following contracts state properties reflecting control laws that should always
        --       hold no matter what incoming event triggers dispatch.   Therefore, they are stated
        --       as independent (not associated with a handler) guarantee clauses.
        --    ---------------------------    
        -- REQ: If current temperature is less than
        --  the current low set point, then the fan state shall be Off
        guarantee currentTempLTSetPoint:
          latestTemp.degrees < currentSetPoint.low.degrees implies currentFanState == FanCmd.Off  
        
        -- REQ: If current temperature is greater than
        --  the current high set point, then the fan state shall be On
        guarantee currentTempGTSetPoint:
          latestTemp.degrees > currentSetPoint.high.degrees implies currentFanState == FanCmd.On  
        
        -- REQ: If current temperature is greater than or equal to the 
        --  current low set point and less than or equal to the current high set point, 
        --  then the current fan state is maintained.
        guarantee currentTempInRange:
          latestTemp.degrees >= currentSetPoint.low.degrees and latestTemp.degrees <= currentSetPoint.high.degrees
          -- if we are still in set point range, don't change the fan state
          implies currentFanState == In(currentFanState)
        
        guarantee newFanState:
          -- if we updated the local record of the fan state state, then we 
          -- must send out a new fan command.
          In(currentFanState) != currentFanState implies MustSend[fanCmd|currentFanState] -- alternate: fanCmd!!currentFanState
        
        --    ---------------------------
        --     Compute Entry Point Handler Contracts
        --       Handler contracts allow the compute entry point contracts to be extended with additional
        --       constraints that must hold for a particular handler.
        --   
        --       At the Slang level, each handler's contract is formed by conjoining the compute entry point 
        --       general clauses with the contract clauses given in the handlers.
        --       For the modifies clauses, CURRENTLY, the modifies clauses must be identical to the modifies
        --       clause in the compute entry point general clauses.  Note this means that the modifies may be
        --       an over-approximation -- some listed variables may actually not be modified in the handler.
        --       (I believe SPARK would give an error/warning if a listed variable is not written on at least
        --        one path, but Logika does not enforce that).    
        --       Thus, in the current state of the tools, if a variable is not actually modified
        --       in the handler, an explicit frame condition must be given equating the pre- and post-state 
        --       version of the variable. 
        --       In the future, such frame conditions may be automatically generated.
        --    ---------------------------   
        --
        handle setPoint:
          modifies currentSetPoint, currentFanState, latestTemp;
          guarantee "setPointChanged":
            currentSetPoint == setPoint;
          guarantee "lastTempNotModified":
            latestTemp == In(latestTemp);
          
        handle tempChanged:
         modifies currentSetPoint, currentFanState, latestTemp;
            guarantee "tempChanged":
              latestTemp == currentTemp;
            guarantee "setPointNotModified":
              currentSetPoint == In(currentSetPoint);
          
        handle fanAck:
          modifies currentSetPoint, currentFanState, latestTemp;  // ToDo: Consider if we need these modifies (could we do something smart?)
          guarantee "setPointNotModified":
            currentSetPoint == In(currentSetPoint);
          guarantee "lastTempNotModified":
            latestTemp == In(latestTemp);
          guarantee "currentFanState":
            currentFanState == In(currentFanState);
      **};
      
    annex GUMBO {**
      -- state variables are not needed in periodic version since we will base all control logic
      --  directly off of data port values 
      state
        currentSetPoint: SetPoint.i;
        currentFanState: CoolingFan::FanCmd;
        latestTemp: TempSensor::Temperature.i;
        
      integration
        assume currentTempRange:
          (currentTemp.degrees >= f32"-70.0") & (currentTemp.degrees <= f32"180.0");
          
      initialize
        modifies currentSetPoint, currentFanState, latestTemp;
        guarantee defautSetPoint:
          (currentSetPoint.low.degrees == f32"70.0") && (currentSetPoint.high.degrees == f32"80.0");
        guarantee defaultFanStates:
          currentFanState == CoolingFan::FanCmd.Off;
        guarantee defaultLatestTemp:
          latestTemp.degrees == f32"72.0";
        
      compute  -- behaviors for COMPUTE Entrypoint
        modifies currentSetPoint, currentFanState, latestTemp;
        
        assume
        	a1 "If the previously received currentTemp was less than the previously
        	   |received setPoint then the last fan command must have been Off":
        	  ((latestTemp.degrees < currentSetPoint.low.degrees) ->: (currentFanState == CoolingFan::FanCmd.Off));

		assume
			a2 "If the previously received currentTemp was more than the previously
        	   |received setPoint then the last fan command must have been On":
              ((latestTemp.degrees > currentSetPoint.high.degrees) ->: (currentFanState == CoolingFan::FanCmd.On));
              
        assume Test "Just testing assume+guarantee along with handlers":
          (3 + 2) == 5;
          
        --    ---------------------------
        --     Compute Entry Point General Clauses
        --       The following contracts state properties reflecting control laws that should always
        --       hold no matter what incoming event triggers dispatch.   Therefore, they are stated
        --       as independent (not associated with a handler) guarantee clauses.
        --    ---------------------------            
        guarantee  TC_Req_01 "If the current temperature is less than the set point, then the fan state shall be Off.":
          (latestTemp.degrees < currentSetPoint.low.degrees) ->: (currentFanState == CoolingFan::FanCmd.Off);  
      
        guarantee TC_Req_02 "If the current temperature is greater than the set point,
                            |then the fan state shall be On.":
          (latestTemp.degrees > currentSetPoint.high.degrees) ->: (currentFanState == CoolingFan::FanCmd.On);  
      
        guarantee TC_Req_03 "If the current temperature is greater than or equal to the
                            |current low set point and less than or equal to the current high set point,
                            |then the current fan state is maintained.":
          ((latestTemp.degrees >= currentSetPoint.low.degrees) & 
            (latestTemp.degrees <= currentSetPoint.high.degrees))
            -- if we are still in set point range, don't change the fan state
            ->: (currentFanState == In(currentFanState));
        
        guarantee mustSendFanCmd "If the local record of the fan state was updated, 
                                 |then send a fan command event with this updated value.":
          ((In(currentFanState) != currentFanState) ->: MustSend(fanCmd,currentFanState)) && -- alternate: fanCmd!!currentFanState

          -- belt: TODO discuss addition of NoSend 
          ((currentFanState == In(currentFanState)) ->: NoSend(fanCmd)) &&
          
          -- belt: testing MustSend with EventDataPort where we just care that an event was sent 
          ((In(currentFanState) != currentFanState) ->: MustSend(fanCmd));

          
          -- belt: below was the original version.  The issue is fanCmd is an api call and the spec
          -- var's type is Option[FanCmd.Type] so a possible rewrite is "api.fanCmd.get", but that
          -- requires a bigger rewrite "api.fanCmd.nonEmpty && (api.fanCmd.get == currentFanState)"
          -- This is a trivial rewrite/replacement with MustSend

          --(currentFanState != In(currentFanState)) ->: (fanCmd == currentFanState);
            
          -- TODO: but, what if instead the user writes something like fanCmd.degrees == f32"70.0"?
          

        -- TODO: discussion, is MustSend supported for outgoing data ports 

        --    ---------------------------
        --     Compute Entry Point Handler Contracts
        --       Handler contracts allow the compute entry point contracts to be extended with additional
        --       constraints that must hold for a particular handler.
        --   
        --       At the Slang level, each handler's contract is formed by conjoining the compute entry point 
        --       general clauses with the contract clauses given in the handlers.
        --       For the modifies clauses, CURRENTLY, the modifies clauses must be identical to the modifies
        --       clause in the compute entry point general clauses.  Note this means that the modifies may be
        --       an over-approximation -- some listed variables may actually not be modified in the handler.
        --       (I believe SPARK would give an error/warning if a listed variable is not written on at least
        --        one path, but Logika does not enforce that).    
        --       Thus, in the current state of the tools, if a variable is not actually modified
        --       in the handler, an explicit frame condition must be given equating the pre- and post-state 
        --       version of the variable. 
        --       In the future, such frame conditions may be automatically generated.
        --    ---------------------------   

        -- TODO: it would be nice if we supported task markers (ie. this would show up in the Tasks view)
        
        -- FIXME: scoping rules do not allow currentTemp to be resolved which could be confusing.
        --        Perhaps do permissive scoping but then have a quick check phase that 
        --        rejects handlers for data ports, outgoing ports, etc
        --handle currentTemp:
        --  modifies currentSetPoint;
        --  guarantee gid: T;
          
        handle setPoint:
          modifies (currentSetPoint);
            
          guarantee setPointChanged:
            currentSetPoint == setPoint;
            
          guarantee latestTempNotModified:
            (latestTemp == In(latestTemp)); 
            
            -- FIXME: scoping does not support the rhs select exp in this alt version
            -- && (latestTemp.degrees == In(latestTemp).degrees);
 
        handle tempChanged:
          modifies (latestTemp);
           
          guarantee tempChanged:
            latestTemp == currentTemp;
            
          guarantee setPointNotModified:
            currentSetPoint == In(currentSetPoint);
            
        handle fanAck:
        
          guarantee setPointNotModified:
            currentSetPoint == In(currentSetPoint);
        
          guarantee lastTempNotModified:
            latestTemp == In(latestTemp);
        
          guarantee currentFanState:
            --currentFanState == fanCmd; -- belt: according to Figure 38, but fanCmd should be empty
            currentFanState == In(currentFanState);
            
          guarantee noEventsSent:
            NoSend(fanCmd);
    **};
      
  end TempControl;

  thread implementation TempControl.s
    -- typically, HAMR thread components have no information specified in their implementation, 
    -- which indicates (a) no further architecture model (the thread is a leaf node in the architecture)
    -- and (b) the thread implementation will be realized at the programming language level.
  end TempControl.s;

  -- ===========================================================================================================
  --
  --   O p e r a t o r   I n t e r f a c e   Thread
  --
  --   The OperatorInterface thread implements a simple UI that
  --      - reports the current temperature to the operator
  --      - accepts set points for the desired temperature range
  -- 
  -- ===========================================================================================================
  thread OperatorInterface
    features
      -- ==== INPUTS ==== 
      currentTemp: in data port TempSensor::Temperature.i; -- current temperature as detected by temp sensor
      tempChanged: in event port; -- notification of temperature change
      -- ==== OUTPUTS ====       
      setPoint: out event data port SetPoint.i;
      -- TODO: consider if this should be removed and added later in an exercise
      -- clearAlarm: out event port;
    flows
      -- OperatorInterface acts as a sink to temperature info because that info is simply
      -- presented on the interface (not sent to other components).  If we added component features
      -- to model interactions with the environment, we could change the sinks to be flow paths -- flowing
      -- temperature info into the environment (e.g., to an entity modeling an operator).
      ct2sink: flow sink currentTemp;
      tc2sink: flow sink tempChanged;
      -- OperatorInterface acts as a source for setPoint information.
      -- If we added component features to model interactions with the environment, we could
      -- change the sinks to be flow paths -- flowing setPoint info from the environment 
      -- (e.g., from an entity modeling an operator) into the system.
      source2sp: flow source setPoint;
    properties
      Dispatch_Protocol => Periodic;
      Period => 1 sec;
  end OperatorInterface;

  thread implementation OperatorInterface.s
    -- typically, HAMR thread components have no information specified in their implementation, 
    -- which indicates (a) no further architecture model (the thread is a leaf node in the architecture)
    -- and (b) the thread implementation will be realized at the programming language level.
  end OperatorInterface.s;



  -- ===========================================================================================================
  --
  --   P E R I O D I C   I M P L E M E N T A T I O N
  --
  -- ===========================================================================================================
  system implementation TempControlSoftwareSystem.p
    -- ==== S u b - c o m p o n e n t s ==== 
    subcomponents
      tcproc: process TempControlProcess.p; -- use implementation of the process with periodic subcomponents
    annex resolute {**
      check HAMR_Level_1
    **};
  end TempControlSoftwareSystem.p;

-- ==========================================================================
--  T e m p C o n t r o l P r o c e s s    (Periodic)
--
--    An alternate version of the process above that 
--     - changes TempControl and Fan threads to have Periodic dispatch protocol
--     - drops the tempChange event notification from TempSensor
--        (we will interpret the currentTemp value as always being an "up-to-date"
--        reading from the TempSensor
-- ==========================================================================
  process implementation TempControlProcess.p -- use "p" for periodic implementation
  -- ==== S u b - c o m p o n e n t s ====  
    subcomponents
      tempSensor: thread TempSensor::TempSensorPeriodic.p; -- use qualified name to refer to element in different package 
      fan: thread CoolingFan::FanPeriodic.p;
      tempControl: thread TempControlPeriodic.p;
      operatorInterface: thread OperatorInterfacePeriodic.p;
    -- ==== C o n n e c t i o n s  ==== 
    connections
      ctTStoTC: port tempSensor.currentTemp -> tempControl.currentTemp;
      ctTStoOI: port tempSensor.currentTemp -> operatorInterface.currentTemp;
      fcTCtoF: port tempControl.fanCmd -> fan.fanCmd;
      faFtoTC: port fan.fanAck -> tempControl.fanAck;
      spOItoTC: port operatorInterface.setPoint -> tempControl.setPoint;
    flows
      -- The intent for the end-to-end flow below is to capture the control loop from the 
      -- temp sensor (sensing the environment) to the fan actuation (acting on the environment).
      -- This can be augmented with a latency constraint -- capturing constraints on the time
      -- from when temperature is sensed to the time when is it evaluated. 
      fanActuation: end to end flow tempSensor.cto -> ctTStoTC -> tempControl.ct2fc -> fcTCtoF -> fan.fc2sink;
      -- Capture the control loop from the setPoint expressed by the operator through the
      -- Operator Interface to the impact of a changed set point on environment actions
      -- (i.e., actuation of the fan).    
      setPointEffect: end to end flow operatorInterface.source2sp -> spOItoTC -> tempControl.sp2fc -> fcTCtoF -> fan.fc2sink;
  end TempControlProcess.p;

-- ===========================================================================================================
  --
  --   T e m p C o n t r o l   ** Periodic **  Thread
  --
  --   A variant of the component above that has a Periodic dispatch protocol.
  --   The purpose of these variants to to illustrate different styles of Compute Entry Point contracts
  --   as well as trade-offs in end-to-end verification.
  -- 
  -- ===========================================================================================================
  thread TempControlPeriodic
    features
      -- ==== INPUTS ==== 
      currentTemp: in data port TempSensor::Temperature.i;
      fanAck: in data port CoolingFan::FanAck;
      setPoint: in data port SetPoint.i;
      -- ==== OUTPUTS ==== 
      fanCmd: out data port CoolingFan::FanCmd;
    flows
      -- currentTemp, tempChanged, and setPoint all influence the value/action of the fanCmd, so there
      -- is a flow path from each to fanCmd.
      ct2fc: flow path currentTemp -> fanCmd; -- data flow from currentTemp to fanCmd
      sp2fc: flow path setPoint -> fanCmd; -- data flow - setPoint value influences value of fanCmd
      -- fanAck is processed internally for the moment (through logging) -- there is no feedback to the UI.
      -- So the TempControl component acts as an information sink for the fanAck.
      fa2sink: flow sink fanAck;
    properties
      Dispatch_Protocol => Periodic;
      Period => 1 sec;

    annex GUMBO {**       

      -- Doc: discuss why since we read/write values directly to data ports that we don't need
      --  all the state variables used in the sporadic version.
      state
        latestFanCmd: CoolingFan::FanCmd;  -- Doc: explain why this is used in case where we maintain state of fan
    
      -- Doc: discuss that since integration constraints are invariants on values flowing across ports,
      --  that even though dispatch protocol has changed, we can leave integration constraints the same.             
      integration   
        assume currentTempRange:
          (currentTemp.degrees >= f32"-70.0") & (currentTemp.degrees <= f32"80.0");
          
      initialize
         modifies (latestFanCmd);
         guarantee initLatestFanCmd "Initialize state variable":
          -- provide initial value for state variable and out data port
          latestFanCmd == CoolingFan::FanCmd.Off;
        
         guarantee initFanCmd "Initial fan command":
           -- provide initial value for state variable and out data port
           fanCmd == CoolingFan::FanCmd.Off;
     
     -- illustrate cases syntax     
      compute  
        modifies (latestFanCmd); 

          guarantee altCurrentTempLTSetPoint "If current temperature is less than
                                             |the current low set point, then the fan state shall be Off" : 
            (currentTemp.degrees < setPoint.low.degrees) ->:  
               ((latestFanCmd == CoolingFan::FanCmd.Off) && (fanCmd == CoolingFan::FanCmd.Off));

          guarantee altCurrentTempGTSetPoint "If current temperature is greater than
                                             |the current high set point, then the fan state shall be On":
             (currentTemp.degrees > setPoint.high.degrees) ->:
               ((latestFanCmd == CoolingFan::FanCmd.On) & (fanCmd == CoolingFan::FanCmd.On));

          guarantee altCurrentTempInRange "If current temperature is greater than or equal to the 
                                          |current low set point and less than or equal to the current high set point, 
                                          |then the current fan state is maintained." :
            ((currentTemp.degrees >= setPoint.low.degrees) & (currentTemp.degrees <= setPoint.high.degrees)) ~~>:
              ((latestFanCmd == In(latestFanCmd)) & (fanCmd == latestFanCmd));

        -- alternative style using cases
                           
        cases
          case currentTempLTSetPoint "If current temperature is less than
                                     |the current low set point, then the fan state shall be Off":
            assume currentTemp.degrees < setPoint.low.degrees; 
            guarantee (latestFanCmd == CoolingFan::FanCmd.Off) & (fanCmd == CoolingFan::FanCmd.Off);

          case currentTempGTSetPoint "If current temperature is greater than
                                     |the current high set point, then the fan state shall be On":
            assume currentTemp.degrees > setPoint.high.degrees; 
            guarantee (latestFanCmd == CoolingFan::FanCmd.On) & (fanCmd == CoolingFan::FanCmd.On);

          case currentTempInRange "If current temperature is greater than or equal to the 
                                  |current low set point and less than or equal to the current high set point, 
                                  |then the current fan state is maintained.":
            assume (currentTemp.degrees >= setPoint.low.degrees) & (currentTemp.degrees <= setPoint.high.degrees);
            guarantee 
              (latestFanCmd == In(latestFanCmd)) & 
              (fanCmd == latestFanCmd);
     **};     
  end TempControlPeriodic;

  thread implementation TempControlPeriodic.p
  end TempControlPeriodic.p;

  -- ===========================================================================================================
  --
  --   O p e r a t o r   I n t e r f a c e   Thread  (for Periodic system context)
  --
  --     Alternate version that 
  --       - eliminates the tempChanged port
  --       - changes setPoint port from event data to data 
  -- 
  -- ===========================================================================================================
  thread OperatorInterfacePeriodic
    features
      -- ==== INPUTS ==== 
      currentTemp: in data port TempSensor::Temperature.i; -- current temperature as detected by temp sensor
      -- ==== OUTPUTS ====       
      setPoint: out data port SetPoint.i;
    -- TODO: consider if this should be removed and added later in an exercise
    -- clearAlarm: out event port;
    flows
      -- OperatorInterface acts as a sink to temperature info because that info is simply
      -- presented on the interface (not sent to other components).  If we added component features
      -- to model interactions with the environment, we could change the sinks to be flow paths -- flowing
      -- temperature info into the environment (e.g., to an entity modeling an operator).
      ct2sink: flow sink currentTemp;
      -- OperatorInterface acts as a source for setPoint information.
      -- If we added component features to model interactions with the environment, we could
      -- change the sinks to be flow paths -- flowing setPoint info from the environment 
      -- (e.g., from an entity modeling an operator) into the system.
      source2sp: flow source setPoint;
    properties
      Dispatch_Protocol => Periodic;
      Period => 1 sec;
  end OperatorInterfacePeriodic;

  thread implementation OperatorInterfacePeriodic.p
  end OperatorInterfacePeriodic.p;


-- ************************************************************************************
--
--  A l t e r n a t e    A r c h i t e c t u r e
--
--  The definitions below represent an alternate architecture for the system in
--  which the operator interface is place in a separate process from the 
--  control system components (sensor, controller, actuator).  The pedagogical purpose
--  of this is to illustrate the following AADL concepts 
--    - multiple implementations of a component type
--    - multiple instance models of a system
--    - connections from a subcomponent port to an enclosing component interface
--
-- ************************************************************************************


  -- ===========================================================================================================
  --   C o n t r o l S y s t e m P r o c e s s 
  --   
  --   Process incorporating control system components 
  --   (operator interface in a separate process) 
  -- 
  -- ===========================================================================================================
  -- ===========================================================================================================
  --   T e m p C o n t r o l S y s t e m
  --
  --   (Alternate architecture with isolated control)   
  -- 
  -- ===========================================================================================================
  system implementation TempControlSoftwareSystem.isoControl
    -- ==== S u b - c o m p o n e n t s ==== 
    subcomponents
      csproc: process ControlSystemProcess.s;
      oiproc: process OperatorInterfaceProcess.s;
    connections
      ctCSPtoOIP: port csproc.currentTemp -> oiproc.currentTemp;
      tcCSPtoOIP: port csproc.tempChanged -> oiproc.tempChanged;
      spOIPtoCSP: port oiproc.setPoint -> csproc.setPoint;
  end TempControlSoftwareSystem.isoControl;

  process ControlSystemProcess
    features
      currentTemp: out event data port TempSensor::Temperature.i;
      tempChanged: out event port;
      setPoint: in event data port SetPoint.i;
  end ControlSystemProcess;

  process implementation ControlSystemProcess.s
    -- ==== S u b - c o m p o n e n t s ====  
    subcomponents
      tempSensor: thread TempSensor::TempSensor.s; -- use qualified name to refer to element in different package 
      fan: thread CoolingFan::Fan.s;
      tempControl: thread TempControl.s;
    -- ==== C o n n e c t i o n s  ==== 
    connections
      ctTStoTC: port tempSensor.currentTemp -> tempControl.currentTemp;
      ctTStoOI: port tempSensor.currentTemp -> currentTemp;
      tcTStoTC: port tempSensor.tempChanged -> tempControl.tempChanged;
      tcTStoOI: port tempSensor.tempChanged -> tempChanged;
      fcTCtoF: port tempControl.fanCmd -> fan.fanCmd;
      faFtoTC: port fan.fanAck -> tempControl.fanAck;
      spOItoTC: port setPoint -> tempControl.setPoint;
  end ControlSystemProcess.s;

  -- ===========================================================================================================
  --   C o n t r o l S y s t e m P r o c e s s 
  --   
  --   Process incorporating control system components 
  --   (operator interface in a separate process) 
  -- 
  -- ===========================================================================================================
  process OperatorInterfaceProcess
    features
      currentTemp: in event data port TempSensor::Temperature.i;
      tempChanged: in event port;
      setPoint: out event data port SetPoint.i;
  end OperatorInterfaceProcess;

  process implementation OperatorInterfaceProcess.s
    -- ==== S u b - c o m p o n e n t s ====  
    subcomponents
      operatorInterface: thread OperatorInterface.s;
    -- ==== C o n n e c t i o n s  ==== 
    connections
      ctOIPtoOI: port currentTemp -> operatorInterface.currentTemp;
      tcOIPtoOI: port tempChanged -> operatorInterface.tempChanged;
      spOItoOIP: port operatorInterface.setPoint -> setPoint;
  end OperatorInterfaceProcess.s;
  
  
    -- ===========================================================================================================
  --
  --   D a t a    T y p e s   for Temp Control System
  --
  --   The system integrator introduces these types to specify format of data communicated between
  --   components that they author.
  --
  --   Other data types used in the system are provided by suppliers of components such as the 
  --   TempSensor and CoolingFan.  These are included in the corresponding included packages.
  --
  --   NOTE: Each data type in HAMR model must conform to the AADL Data Modeling Annex.  See HAMR
  --   documentation chapter on Data Types for details.
  -- 
  -- ===========================================================================================================
  -- ----------------------------------------------------------------------------------
  --
  --   S e t  P o i n t   Data Type   
  --
  --   Define the structure of set point information (desired temperature range)
  -- ----------------------------------------------------------------------------------
  data SetPoint
    -- Indicate that the set point info will be a struct (record)
    -- For structs, use of the type must refer to the data component implementation (SetPoint.i),
    --  which defines the fields and associated types of the struct.
    properties
      Data_Model::Data_Representation => Struct;
  end SetPoint;

  data implementation SetPoint.i
    subcomponents
      low: data TempSensor::Temperature.i;
      high: data TempSensor::Temperature.i;
    annex GUMBO {**
      invariants
        inv SetPoint_Data_Invariant:
          (low.degrees >= f32"50.0") &
          (high.degrees <= f32"110.0") &
          (low.degrees <= high.degrees);
    **};
  end SetPoint.i;
  

end TempControlSoftwareSystem;